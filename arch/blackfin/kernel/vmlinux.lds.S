/*
 *	vmlinux.lds.S - Master linker script for blackfin architecture
 *
 *	(C) Copyright 2004-2005 LG Soft India 
 *
 */
#define VMLINUX_SYMBOL(_sym_) _##_sym_

#include <asm-generic/vmlinux.lds.h>
#include <linux/config.h>
#include <asm/mem_map.h>


OUTPUT_FORMAT("elf32-bfin")
ENTRY(__start) 
_jiffies = _jiffies_64;
MEMORY 
	{
	ram		: ORIGIN = RAM_START,       LENGTH = RAM_LENGTH
	l1_data_a	: ORIGIN = L1_DATA_A_START, LENGTH = L1_DATA_A_LENGTH
	l1_data_b	: ORIGIN = L1_DATA_B_START, LENGTH = L1_DATA_B_LENGTH
	l1_code		: ORIGIN = L1_CODE_START,   LENGTH = L1_CODE_LENGTH
	l1_scratch	: ORIGIN = L1_SCRATCH_START,LENGTH = L1_SCRATCH_LENGTH
	}

SECTIONS
{
	. = RAM_START ;

	.text  : {
		 __stext = .;
		*(.text)
		SCHED_TEXT 
		*(.text.lock)
		. = ALIGN(16);
		  ___start___ex_table = .;
		*(__ex_table)
		 ___stop___ex_table = .;

		  *($code)
        	  *(.rodata)
        	  *(.rodata.*)
		  *(__vermagic)		/* Kernel version magic */
        	  *(.rodata1)
	          *(.fixup)	

		/* Kernel symbol table: Normal symbols */
		. = ALIGN(4);
		___start___ksymtab = .;
		*(__ksymtab)
		___stop___ksymtab = .;

		/* Kernel symbol table: GPL-only symbols */
		___start___ksymtab_gpl = .;
		*(__ksymtab_gpl)
		___stop___ksymtab_gpl = .;

		/* Kernel symbol table: Normal symbols */
		___start___kcrctab = .;
		*(__kcrctab)
		___stop___kcrctab = .;

		/* Kernel symbol table: GPL-only symbols */
		___start___kcrctab_gpl = .;
		*(__kcrctab_gpl)
		___stop___kcrctab_gpl = .;

		/* Kernel symbol table: strings */
		*(__ksymtab_strings)

		 . = ALIGN(4) ;
	  	__etext = . ;
	} > ram
   
	.init : 
	{
		. = ALIGN(4096);
		___init_begin = .;
		__sinittext = .;
		*(.init.text)
		__einittext = .;
		*(.init.data)
		. = ALIGN(16);
		___setup_start = .;
		*(.init.setup)
		___setup_end = .;
		___start___param = .;
		*(__param)
		___stop___param = .;
		___initcall_start = .;
		*(.initcall1.init)
		*(.initcall2.init)
		*(.initcall3.init)
		*(.initcall4.init)
		*(.initcall5.init)
		*(.initcall6.init)
		*(.initcall7.init)
		___initcall_end = .;
		___con_initcall_start = .;
		*(.con_initcall.init)
		___con_initcall_end = .;
		___security_initcall_start = .;
		*(.security_initcall.init)
		___security_initcall_end = .;
		. = ALIGN(4);
		___initramfs_start = .;
		*(.init.ramfs)
		___initramfs_end = .;
		. = ALIGN(4);
		___init_end = .;
	} > ram

	 __l1_lma_start = .;                     
	.text_l1 L1_CODE_START : 
		AT ( __l1_lma_start ) 
	{
		. = ALIGN(4) ;
		 __stext_l1 = . ;
		*(.text.l1)
	
		. = ALIGN(4) ;
		 __etext_l1 = . ;  
	} 

	.data_l1 L1_DATA_A_START :
		AT ( __l1_lma_start + SIZEOF(.text_l1) )
	{
		. = ALIGN(4) ;
		 __sdata_l1 = . ;
		*(.data.l1)
		 __edata_l1 = . ;  

		. = ALIGN(4) ;
		 __sbss_l1 = . ;
		*(.bss.l1)

		. = ALIGN(4) ;
		 __ebss_l1 = . ;  
	} 

	. = __l1_lma_start + SIZEOF(.text_l1) + SIZEOF(.data_l1) ;
	.data (__l1_lma_start + SIZEOF(.text_l1) + SIZEOF(.data_l1)) :	AT ( __l1_lma_start + SIZEOF(.text_l1) + SIZEOF(.data_l1) )
	{
		 __sdata = . ;
		. = ALIGN(0x2000) ;
		*(.data.init_task)
		*(.data)
		__edata = . ;
	} > ram

	/DISCARD/ : {			/* Exit code and data*/
		*(.exit.text)
		*(.exit.data)
		*(.exitcall.exit)
	} > ram


	.bss : AT ( __l1_lma_start + SIZEOF(.text_l1) + SIZEOF(.data_l1) + SIZEOF(.data) )
	{
		. = ALIGN(4) ;
		 __sbss = . ;
		*(.bss)
		*(COMMON)
		. = ALIGN(4) ;
		 __ebss = . ;	  
		 __end = . ;	  
 		__bss_end = . ;
    	} > ram


#ifdef CONFIG_FULLDEBUG
  /* Stabs debugging sections.  */
  .stab          0 : { *(.stab) }
  .stabstr       0 : { *(.stabstr) }
  .stab.excl     0 : { *(.stab.excl) }
  .stab.exclstr  0 : { *(.stab.exclstr) }
  .stab.index    0 : { *(.stab.index) }
  .stab.indexstr 0 : { *(.stab.indexstr) }
  .comment       0 : { *(.comment) }
  /* DWARF debug sections.
     Symbols in the DWARF debugging sections are relative to the beginning
     of the section so we begin them at 0.  */
  /* DWARF 1 */
  .debug          0 : { *(.debug) }
  .line           0 : { *(.line) }
  /* GNU DWARF 1 extensions */
  .debug_srcinfo  0 : { *(.debug_srcinfo) }
  .debug_sfnames  0 : { *(.debug_sfnames) }
  /* DWARF 1.1 and DWARF 2 */
  .debug_aranges  0 : { *(.debug_aranges) }
  .debug_pubnames 0 : { *(.debug_pubnames) }
  /* DWARF 2 */
  .debug_info     0 : { *(.debug_info .gnu.linkonce.wi.*) }
  .debug_abbrev   0 : { *(.debug_abbrev) }
  .debug_line     0 : { *(.debug_line) }
  .debug_frame    0 : { *(.debug_frame) }
  .debug_str      0 : { *(.debug_str) }
  .debug_loc      0 : { *(.debug_loc) }
  .debug_macinfo  0 : { *(.debug_macinfo) }
  /* SGI/MIPS DWARF 2 extensions */
  .debug_weaknames 0 : { *(.debug_weaknames) }
  .debug_funcnames 0 : { *(.debug_funcnames) }
  .debug_typenames 0 : { *(.debug_typenames) }
  .debug_varnames  0 : { *(.debug_varnames) }
  /DISCARD/ : { *(.note.GNU-stack) }
#else
  /DISCARD/ : { *(.comment) }
#endif

}
